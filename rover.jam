
GOALS:
  ACHIEVE FindAllWaterSources;
FACTS:
  FACT MAX_XY 7 6;

  FACT ROCK 2 6 "False";
  FACT ROCK 3 1 "False";
  FACT ROCK 6 1 "False";
  FACT ROCK 5 3 "False";
  FACT ROCK 7 5 "True";
  FACT ROCK 3 2 "True";
  FACT ROCK 2 2 "True";
  FACT ROCK 3 3 "True";

  FACT EXPLORATORY_MODE "ON";
  FACT MOVING_HOME_MODE "OFF";
  FACT FETCH_MODE "OFF";

  FACT EXPLOR_START_POS 0 0;
  FACT CURR_POS 0 0;
  FACT EXPL_VELOCITY 1;

  FACT TOTAL_MOVES 0;
  FACT WATER_FOUND "";
  FACT EMPTY_ROCK_FOUND "";

// Find water source
Plan: {
  NAME:
  	"Find Water Source"
  GOAL:
  	ACHIEVE FindAllWaterSources;
  BODY:
      OR {
        TEST(FACT MOVING_HOME_MODE "ON");
        PERFORM MovedTo 0 0 "in base\n";
        UPDATE (MOVING_HOME_MODE) (MOVING_HOME_MODE "OFF");
        RETRIEVEALL $knowns KNOWN_ROCK_LOC $x $y;
        NEXTFACT $knowns KNOWN_ROCK_LOC $x $y;
        UPDATE (FETCH_MODE) (FETCH_MODE "ON");
        FAIL;
      }{
        TEST(FACT FETCH_MODE "ON");
        PERFORM FetchKnownRockRoutine;
        FAIL;
      }{
        RETRIEVE EXPLOR_START_POS $sX $sY;
        OR {
          RETRIEVE MAX_XY $MAX_X $MAX_Y;
          OR {
            TEST (== (% $MAX_X 2) 0);
            TEST (&& (== $sX $MAX_X) (== $sY $MAX_Y));
          }{
            TEST (!= (% $MAX_X 2) 0);
            TEST (&& (== $sX $MAX_X) (== $sY 0));
          };
        }{
          RETRIEVE CURR_POS $x $y;
          WHEN: TEST (|| (!= $x $sX) (!= $y $sY)){
            PERFORM MovedTo $sX $sY "at the exploaration point\n";
          };
          UPDATE (EXPLORATORY_MODE) (EXPLORATORY_MODE "ON");
          ACHIEVE ExploratoryRoutine;
        };
      };
      PERFORM PrintStats;
}

// ExploratoryRoutine
Plan: {
  NAME:
  	""
  GOAL:
  	ACHIEVE ExploratoryRoutine $x $y;
  CONTEXT:
    FACT EXPLORATORY_MODE "ON";
  BODY:
    RETRIEVE MAX_XY $MAX_X $MAX_Y;
    RETRIEVE EXPL_VELOCITY $velocity;
    RETRIEVE CURR_POS $x $y;
    WHILE: TEST (|| (!= $x $MAX_X) (!= $y $MAX_Y)){
      ASSIGN $y (+ $y $velocity);
      PERFORM MoveTo $x $y;
      PERFORM CheckAndStore $x $y;
      WHEN: TEST (&& (|| (== $y $MAX_Y) (== $y 0)) (< $x $MAX_X)) {
        ASSIGN $x (+ $x 1); // move to next x axis
        PERFORM MoveTo $x $y;
        PERFORM CheckAndStore $x $y;
        ASSIGN $velocity (* $velocity -1);
        UPDATE (EXPL_VELOCITY) (EXPL_VELOCITY $velocity);
      };
    };
  FAILURE:
    WHEN: TEST (&& (== $y $MAX_Y) (== $velocity 1)){
      UPDATE (EXPL_VELOCITY) (EXPL_VELOCITY -1);
    };
    WHEN: TEST (&& (== $y 0) (== $velocity -1)){
      UPDATE (EXPL_VELOCITY) (EXPL_VELOCITY 1);
    };
}

// FetchKnownRockRoutine
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM FetchKnownRockRoutine;
  BODY:
    RETRIEVEALL $knowns KNOWN_ROCK_LOC $x $y;
    NEXTFACT $knowns KNOWN_ROCK_LOC $x $y;
    EXECUTE print "\nReturning to a known rock\n";
    PERFORM MovedTo $x $y "at a known rock location\n";
    RETRACT KNOWN_ROCK_LOC $x $y;
    PERFORM CheckAndStore $x $y;
    UPDATE (FETCH_MODE) (FETCH_MODE "OFF");
}

// MovedTo
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM MovedTo $X $Y $destName;
  BODY:
    RETRIEVE CURR_POS $x $y;
    AND
    {
      ASSIGN $velocity 1;
      WHEN: TEST (< $X $x) {
        ASSIGN $velocity -1;
      };
      WHILE: TEST (!= $X $x){
        ASSIGN $x (+ $x $velocity);
        PERFORM MoveTo $x $y;
      };
    }{
      ASSIGN $velocity 1;
      WHEN: TEST (< $Y $y) {
        ASSIGN $velocity -1;
      };
      WHILE: TEST (!= $Y $y){
        ASSIGN $y (+ $y $velocity);
        PERFORM MoveTo $x $y;
      };
    };
  EFFECTS:
    EXECUTE print "\nArrived " $destName;
}

// GetHomeRoutine
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM GetHomeRoutine;
  BODY:

}
// GetHomeExploring
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM GetHomeExploring $x $y;
  BODY:
    WHILE: TEST (> $y 0){
      ASSIGN $y (- $y 1); // intend to move down
      PERFORM MoveTo $x $y;
      PERFORM CheckAndRemember $x $y;
    };
    UPDATE (EXPLOR_START_POS) (EXPLOR_START_POS $x $y);
}

// CheckAndStore
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM CheckAndStore $x $y;
  BODY:
    OR{
      TEST (FACT ROCK $x $y "True");
      RETRIEVE WATER_FOUND $wf;
      UPDATE (WATER_FOUND) (WATER_FOUND (+ $wf (+ "\nRock with water at: " (+ (+ $x " | ") $y))));
      EXECUTE print "\nFound rock with water - will take it to base\n";
      RETRACT ROCK $x $y "True"; // delete
      WHEN: TEST (&& (FACT EXPL_VELOCITY -1) (> $y 0)){ // When was exploring downwards
        PERFORM GetHomeExploring $x $y;
      };
      UPDATE (MOVING_HOME_MODE) (MOVING_HOME_MODE "ON");
      UPDATE (EXPLOR_START_POS) (EXPLOR_START_POS $x $y);
      UPDATE (EXPLORATORY_MODE) (EXPLORATORY_MODE "OFF");
    }{
      TEST (FACT ROCK $x $y "False");
      EXECUTE print "\nFound rock with no water - discarding it\n";
      RETRIEVE EMPTY_ROCK_FOUND $str;
      UPDATE (EMPTY_ROCK_FOUND) (EMPTY_ROCK_FOUND (+ $str (+ "\nRock with no water at: " (+ (+ $x " | ") $y))));
      RETRACT ROCK $x $y "False"; // delete
    }{
      SUCCEED;
    };
}
// CheckAndRemember
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM CheckAndRemember $x $y;
  BODY:
    WHEN: TEST (FACT ROCK $x $y){
      EXECUTE print "\nFound another rock, saving its location\n";
      ASSERT KNOWN_ROCK_LOC $x $y;
    };
}
// PrintStats
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM PrintStats ;
  BODY:
    RETRIEVE WATER_FOUND $water ;
    RETRIEVE EMPTY_ROCK_FOUND $noWater;
    RETRIEVE TOTAL_MOVES $n;
    EXECUTE print "\n\nMISSION COMPLETE!";
    EXECUTE print "\nTotal moves were made: " $n "\n";
    EXECUTE print $noWater "\n";
    EXECUTE print $water "\n";

}
// CheckIfRockAround
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM CheckIfRockAround $rockIsFound $x $y;
  BODY:
    ASSIGN $i 0;
    RETRIEVEALL $rocks ROCK $X $Y;

    WHILE: TEST (< $i 5){
      NEXTFACT $rocks ROCK $X $Y;
      WHEN: TEST (&& (== $x $X) (== $y $Y)){
        ASSIGN $rockIsFound "True";
      };
      ASSIGN $i (+ $i 1);
    };
}
// AnalyzeRock
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM AnalyzeRock;
  BODY:
    RETRIEVE initialized $foo;
    EXECUTE print $foo "\n";
    ASSIGN $foo (+ $foo "!!!");
}
// MoveTo X,Y
Plan: {
  NAME:
  	""
  GOAL:
  	PERFORM MoveTo $x $y;
  BODY:
    RETRIEVE TOTAL_MOVES $n;
    UPDATE (TOTAL_MOVES) (TOTAL_MOVES (+ $n 1));
    UPDATE (CURR_POS) (CURR_POS $x $y);
    EXECUTE print "\n-> X: " $x " Y: " $y " | ";
}
